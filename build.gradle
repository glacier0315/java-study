// 配置所有项目公有
subprojects {
    apply plugin: 'java'

    group = 'com.glacier'
    version = '0.0.1'

    // 指定jdk版本
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    // 指定仓库
    repositories {
        mavenLocal()
        maven { url 'https://maven.aliyun.com/repository/public' }
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url 'https://maven.aliyun.com/repository/jcenter' }
        maven { url 'https://maven.aliyun.com/repository/google' }
        maven { url 'https://maven.aliyun.com/repository/spring' }
        maven { url 'https://maven.aliyun.com/repository/spring-plugin' }
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
    }

    dependencies {
        // Use JUnit Jupiter API for testing.
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'

        // Use JUnit Jupiter Engine for testing.
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }

    // 设置编码为UTF-8
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    //配置 删除文件夹
    task cleanTempFiles {
        doLast {
            delete "out"
            delete "logs"
        }
    }
    clean.dependsOn cleanTempFiles

    // 配置 创建源文件夹
    task mkdirs {
        sourceSets*.java.srcDirs*.each {
            it.mkdirs()
        }
        sourceSets*.resources.srcDirs*.each {
            it.mkdirs()
        }
    }

    // 拷贝jar包
    task copyJars(type: Copy) {
        // 运行时
        from configurations.runtimeClasspath
        // 目标位置
        into "$buildDir/libs"
    }

    tasks.named('test') {
        // Use junit platform for unit tests.
        useJUnitPlatform()
    }
}
